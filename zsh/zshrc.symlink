#!/usr/bin/env zsh

# This is the absolute path to this repo's root directory
export DOTFILES="$(realpath $(dirname $(readlink -f ${(%):-%x}))/..)"

# Put all extra setup in the localrc
# E.g. setting env variables not relevant to this repo
if [[ -a ~/.localrc ]]
then
    source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
if [[ $DOTFILES -ef $(realpath ~)/.zshrc ]]; then
    # Not going to run if zsh no longer points to the dotfiles repo
    echo "Installation error. zshrc not symlinked"
    config_files=
else
    config_files=($DOTFILES/**/*.zsh)
fi

# load the path files
for file in ${(M)config_files:#*/path.zsh} ; do
    source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh} ; do
    source $file
done

# Load antidote and plugins
[[ -e ${XDG_CACHE_HOME}/.antidote ]] ||
    git clone --depth=1 https://github.com/mattmc3/antidote.git ${XDG_CACHE_HOME}/.antidote

source ${XDG_CACHE_HOME}/.antidote/antidote.zsh
antidote load "${DOTFILES}/zsh/zsh_plugins.txt" "${XDG_CACHE_HOME}/.zsh_plugins.zsh"

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh} ; do
    source $file
done

unset config_files

eval "$(starship init zsh)"
